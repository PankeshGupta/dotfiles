# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# export the ZDOTDIR for Prezto
export ZDOTDIR=$HOME/.dotfiles/zsh

# your project folder that we can `c [tab]` to
export PROJECTS=~/projects

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

# more defaults
setopt interactivecomments
bindkey "^[[A" history-beginning-search-backward
bindkey "^[[B" history-beginning-search-forward


# . /Users/rachit/torch/install/bin/torch-activate

# OPAM configuration
. /Users/rachit/.opam/opam-init/init.zsh > /dev/null 2> /dev/null || true

# setxkbmap -option caps:swapescape

if (( $+commands[tag] )); then
    tag() { command tag "$@"; source ${TAG_ALIAS_FILE:-/tmp/tag_aliases} 2>/dev/null }
    alias ag=tag
fi

export PYTHONSTARTUP=~/.pythonstartup

# THIS IS BIG 
# aliasing vim to /usr/local/bin/vi
alias vim=/usr/local/bin/vim
alias agenda='gcalcli agenda'
alias ody='ssh -Y -C -o ServerAliveInterval=30 -fN ody'
alias class='cd ~/Documents/class/current/'

# FZF stuff!!!!
export FZF_DEFAULT_OPTS='-m'
fe() {
    # fe - Open the selected files with the default editor
    local files=$(fzf --query="$1" --select-1 --exit-0 | sed -e "s/\(.*\)/\'\1\'/")
    local command="${EDITOR:-vim} -p $files"
    [ -n "$files" ] && eval $command
}

fd() {
    # fd - cd to selected directory
    local dir
    dir=$(find ${1:-*} -path '*/\.*' -prune -o -type d -print 2> /dev/null | fzf +m) &&
    cd "$dir"
}

fh() {
    # fh - repeat history
    eval $( ([ -n "$ZSH_NAME" ] && fc -l 1 || history) | fzf +s --tac | sed 's/ *[0-9]* *//')
}

fkill() {
    # fkill - kill process
    pid=$(ps -ef | sed 1d | fzf -m | awk '{print $2}')
    if [ "x$pid" != "x" ]
    then
        kill -${1:-9} $pid
    fi
}
